#include <SPI.h>
#include <MFRC522.h>
#include <ESP32Servo.h>
#include <HX711.h>
#include <Wire.h>
#include <LiquidCrystal_I2C.h>
#include <WiFi.h>
#include <Firebase_ESP_Client.h>
#include <addons/TokenHelper.h>
#include <addons/RTDBHelper.h>
#include <time.h>


int servoPin = 13;
int closePos = 45;
int openPos  = 0;

#define SS_PIN 2
#define RST_PIN 22

#define DT 4
#define SCK 5

#define TRIG 25
#define ECHO 33

//here, wifi nu naam and passsword 
#define WIFI_SSID "YOUR_WIFI_NAME"
#define WIFI_PASSWORD "YOUR_WIFI_PASSWORD"

#define API_KEY "AIzaSyC-ukbAaOdiEb65v9F7FuBzT0TTRoQHyxw"
#define DATABASE_URL "https://ecosafar-3da96-default-rtdb.firebaseio.com"


FirebaseData fbdo;
FirebaseAuth auth;
FirebaseConfig config;

MFRC522 rfid(SS_PIN, RST_PIN);
HX711 scale;
LiquidCrystal_I2C lcd(0x27, 16, 2);
Servo lidServo;


String currentUID = "";
String userCity = "Anand";

float scaleFactor = 210612;
long offset = 539750;

float Updateweight = 0;
float Ecopoints = 0;

float TotalDistance = 21;
float BinLevel = 0;


void setup() {
  Serial.begin(115200);
  SPI.begin();
  rfid.PCD_Init();

  lidServo.attach(servoPin);
  lidServo.write(closePos);

  scale.begin(DT, SCK);
  scale.set_scale(scaleFactor);

  pinMode(TRIG, OUTPUT);
  pinMode(ECHO, INPUT);

  Wire.begin(27, 26);
  lcd.init();
  lcd.backlight();

  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
  Serial.print("Connecting WiFi");
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("\nWiFi Connected");

  config.api_key = API_KEY;
  config.database_url = DATABASE_URL;
  Firebase.begin(&config, &auth);
  Firebase.reconnectWiFi(true);

  configTime(0, 0, "pool.ntp.org");

  Serial.println("System Ready");
}


long getdistance() {
  digitalWrite(TRIG, LOW);
  delayMicroseconds(2);
  digitalWrite(TRIG, HIGH);
  delayMicroseconds(10);
  digitalWrite(TRIG, LOW);

  long duration = pulseIn(ECHO, HIGH, 25000);
  return (duration * 0.034) / 2;
}


void loop() {

  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("    EcoSafar");

  if (!rfid.PICC_IsNewCardPresent()) return;
  if (!rfid.PICC_ReadCardSerial()) return;

//uid thi string nu
  currentUID = "";
  for (byte i = 0; i < rfid.uid.size; i++) {
    char hex[3];
    sprintf(hex, "%02X", rfid.uid.uidByte[i]);
    currentUID += hex;
  }

  Serial.print("UID: ");
  Serial.println(currentUID);

  String userPath = "/users/" + currentUID;

 //user exist kare ke nai 
  if (!Firebase.RTDB.getString(&fbdo, userPath + "/name")) {
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("Access Denied");
    lcd.setCursor(0, 1);
    lcd.print("Not Registered");
    delay(3000);
    rfid.PICC_HaltA();
    return;
  }

//bin full thayu ke nai
  if (BinLevel >= 85) {
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("Dustbin Full");
    lcd.setCursor(0, 1);
    lcd.print("Thank You");
    delay(4000);
    return;
  }

//lid open maate 
  for (int i = closePos; i >= openPos; i--) {
    lidServo.write(i);
    delay(5);
  }

  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Deposit Waste");
  delay(8000);

//weight measure
  long sumRaw = 0;
  for (int i = 0; i < 20; i++) {
    sumRaw += scale.read();
    delay(10);
  }

  long avgRaw = sumRaw / 20;
  float weightg = ((avgRaw - offset) / scaleFactor) * 1000;
  if (weightg < 0) weightg = 0;

  Updateweight = weightg;
  Ecopoints = Updateweight * 0.1;

  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Weight:");
  lcd.print(Updateweight, 1);
  lcd.print("g");

  lcd.setCursor(0, 1);
  lcd.print("Points:");
  lcd.print(Ecopoints, 1);

  delay(3000);

 //lid close
  for (int j = openPos; j <= closePos; j++) {
    lidServo.write(j);
    delay(5);
  }

//bin level maate
  float Distance = getdistance();
  float diff = TotalDistance - Distance;
  BinLevel = (diff / TotalDistance) * 100;
  if (BinLevel < 0) BinLevel = 0;
  if (BinLevel > 100) BinLevel = 100;

//firebase ma update
  if (Updateweight > 0) {

    unsigned long ts = time(nullptr);
    String historyPath = userPath + "/history/" + String(ts);

    Firebase.RTDB.setFloat(&fbdo, historyPath + "/weight", Updateweight);
    Firebase.RTDB.setFloat(&fbdo, historyPath + "/ecoPoints", Ecopoints);

    float oldW = 0, oldP = 0;
    if (Firebase.RTDB.getFloat(&fbdo, userPath + "/TotalWeight")) oldW = fbdo.floatData();
    if (Firebase.RTDB.getFloat(&fbdo, userPath + "/Ecopoints")) oldP = fbdo.floatData();

    Firebase.RTDB.setFloat(&fbdo, userPath + "/TotalWeight", oldW + Updateweight);
    Firebase.RTDB.setFloat(&fbdo, userPath + "/Ecopoints", oldP + Ecopoints);

    Firebase.RTDB.setFloat(&fbdo, "/bins/" + userCity + "/fillPercentage", BinLevel);
  }

  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Waste Accepted");
  lcd.setCursor(0, 1);
  lcd.print("Thank You");

  delay(4000);
  rfid.PICC_HaltA();
}
